import BiasedBisect.Inv
import BiasedBisect.Multigeometric
import Mathlib.Analysis.Fourier.FourierTransform
import Mathlib.Analysis.SpecialFunctions.ImproperIntegrals
import Mathlib.Analysis.SpecialFunctions.Integrals.Basic
import Mathlib.MeasureTheory.Integral.ExpDecay
import Mathlib.MeasureTheory.Integral.IntegrableOn


open Real
open FourierTransform
open Complex
open MeasureTheory


lemma ne_zero_of_re_neg {s : â„‚} (hs : 0 > s.re) : s â‰  0 :=
  fun h â†¦ (Complex.zero_re â–¸ h â–¸ hs).false

lemma Ï†_grossBound (s t x: â„) (h: x â‰¥ - max s t) [PosReal s] [PosReal t]:
Ï† s t x â‰¤ rexp ((Real.log 2 / (min s t)) * x) * rexp (Real.log 2 / (min s t) * max s t) := by
  apply le_trans (Ï†_bound s t x h).2
  rw [â† Real.exp_add, â† mul_add]
  apply Real.exp_monotone
  refine mul_le_mul_of_nonneg_right ?_ (neg_le_iff_add_nonneg.mp h)
  apply (Ïf_anti s t).le_iff_ge.mp
  rw [Ï_satisfies]
  unfold Ïf
  rw [(by norm_num: (1:â„) = 2â»Â¹ + 2â»Â¹)]
  apply add_le_add
  all_goals
  Â· apply (le_inv_commâ‚€ (by simp) (Real.exp_pos _)).mp
    rw [â† Real.exp_neg, neg_mul, neg_neg, â† mul_div_assoc, mul_comm, mul_div_assoc,
      Real.exp_mul, Real.exp_log (by simp)]
    apply self_le_rpow_of_one_le (by simp)
    apply (one_le_divâ‚€ (lt_min PosReal.pos PosReal.pos)).mpr
    simp

noncomputable
def smStep (Î¼ x: â„): â„ := if x â‰¤ 0 then 0 else if x â‰¤ Î¼ then x / Î¼ else 1

lemma smStepContinuous (Î¼: â„) [PosReal Î¼]: Continuous (smStep Î¼) := by
  apply Continuous.if
  Â· intro x xmem
    rw [â† Set.Iic, frontier_Iic] at xmem
    simp only [Set.mem_singleton_iff] at xmem
    have cond: x â‰¤ Î¼ := by
      rw [xmem]
      exact le_of_lt PosReal.pos
    simp only [cond, â†“reduceIte]
    rw [xmem]
    simp only [zero_div]
  Â· continuity
  Â· apply Continuous.if
    Â· intro x xmem
      rw [â† Set.Iic, frontier_Iic] at xmem
      simp only [Set.mem_singleton_iff] at xmem
      rw [xmem]
      exact div_self (ne_of_gt PosReal.pos)
    Â· continuity
    Â· continuity

lemma smStepLe1 (Î¼ x: â„) [PosReal Î¼]: smStep Î¼ x â‰¤ 1 := by
  unfold smStep
  split_ifs with x0 x1
  Â· norm_num
  Â· exact (div_le_one PosReal.pos).mpr x1
  Â· exact le_rfl

lemma smStepNonneg (Î¼ x: â„) [PosReal Î¼]: 0 â‰¤ smStep Î¼ x := by
  unfold smStep
  split_ifs with x0 x1
  Â· norm_num
  Â· simp only [not_le] at x0
    exact div_nonneg (le_of_lt x0) (le_of_lt PosReal.pos)
  Â· norm_num

noncomputable
def Ï†Reg (s t Î¼ Ïƒ x: â„) := rexp (- Ïƒ * x) * (Set.indicator (Set.Ici 0) (fun _ â†¦ 1) x + âˆ‘' pq,  Jâ‚š pq * (smStep Î¼ (x - (pq.1 * s + pq.2 * t))))

lemma Ï†RegLe (s t Î¼ Ïƒ x: â„) [PosReal s] [PosReal t] [PosReal Î¼]:
Ï†Reg s t Î¼ Ïƒ x â‰¤ rexp (- Ïƒ * x) * Ï† s t x := by
  unfold Ï†Reg
  refine mul_le_mul_of_nonneg_left ?_ (by apply exp_nonneg)
  unfold Ï† Jceiled
  push_cast
  apply add_le_add
  Â· apply Set.indicator_le' (by simp only [Set.mem_Ici, le_refl, implies_true])
      (by simp only [Set.mem_Ici, not_le, zero_le_one, implies_true])
  Â· have shut: âˆ€pq âˆ‰ ((Î›ceiled s t x).toFinset), ((Jâ‚š pq) * smStep Î¼ (x - (â†‘pq.1 * s + â†‘pq.2 * t)): â„) = 0 := by
      intro pq pqnotmem
      unfold Î›ceiled at pqnotmem
      simp only [Set.mem_toFinset, Set.mem_setOf_eq, not_le] at pqnotmem
      obtain range := sub_nonpos_of_le (le_of_lt pqnotmem)
      apply mul_eq_zero_of_right
      unfold smStep
      simp only [range, â†“reduceIte]
    rw [tsum_eq_sum shut]
    gcongr with pq pqmem
    refine (mul_le_iff_le_one_right ?_).mpr (by apply smStepLe1)
    Â· norm_cast
      apply Jâ‚š_nonzero


lemma Ï†Reg_neg (s t Î¼ Ïƒ x: â„) (h: x < 0) [PosReal s] [PosReal t] : Ï†Reg s t Î¼ Ïƒ x = 0 := by
  unfold Ï†Reg
  apply mul_eq_zero_of_right
  simp only [Set.mem_Ici, not_le, h, Set.indicator_of_notMem, zero_add]
  convert tsum_zero with pq
  apply mul_eq_zero_of_right
  unfold smStep
  have cond: x - (pq.1 * s + pq.2 * t) â‰¤ 0 := by
    apply sub_nonpos_of_le
    apply le_of_lt
    apply lt_of_lt_of_le h
    apply add_nonneg
    all_goals
    Â· apply mul_nonneg
      Â· simp only [Nat.cast_nonneg]
      Â· exact le_of_lt PosReal.pos
  simp only [cond, â†“reduceIte]


lemma Ï†RegBound (s t Î¼ Ïƒ x: â„) [PosReal s] [PosReal t] [PosReal Î¼]:
Ï†Reg s t Î¼ Ïƒ x â‰¤ rexp ((Real.log 2 / (min s t) - Ïƒ) * x) * rexp (Real.log 2 / (min s t) * max s t) := by
  obtain xneg|xpos := lt_or_ge x 0
  Â· rw [Ï†Reg_neg _ _ _ _ _ xneg]
    exact mul_nonneg (by apply exp_nonneg) (by apply exp_nonneg)
  Â· rw [sub_eq_neg_add, add_mul, Real.exp_add, mul_assoc]
    apply le_trans (Ï†RegLe s t Î¼ Ïƒ x)
    refine mul_le_mul_of_nonneg_left ?_ (by apply exp_nonneg)
    apply Ï†_grossBound
    apply le_of_lt
    refine lt_of_lt_of_le ?_ xpos
    simp only [Left.neg_neg_iff, lt_sup_iff]
    left; exact PosReal.pos

lemma Ï†RegNonneg (s t Î¼ Ïƒ x: â„) [PosReal s] [PosReal t] [PosReal Î¼]:
0 â‰¤ Ï†Reg s t Î¼ Ïƒ x := by
  unfold Ï†Reg
  apply mul_nonneg (by apply exp_nonneg)
  apply add_nonneg
  Â· apply Set.indicator_nonneg
    simp only [Set.mem_Ici, zero_le_one, implies_true]
  Â· apply tsum_nonneg
    intro pq
    apply mul_nonneg (by apply Nat.cast_nonneg')
    apply smStepNonneg

lemma JceiledContinuous (s t Î¼ : â„) [PosReal s] [PosReal t] [PosReal Î¼]:
Continuous (fun x â†¦ âˆ‘' (pq : â„• Ã— â„•), Jâ‚š pq * smStep Î¼ (x - (pq.1 * s + pq.2 * t)))  := by
  let coverSet := fun (x:â„) â†¦ Set.Iio (x + 1)
  have converSetMap: âˆ€ (x:â„), âˆƒ (i:â„), coverSet i âˆˆ nhds x := by
    intro x
    use x
    unfold coverSet
    refine Iio_mem_nhds ?_
    exact lt_add_one x
  apply continuous_of_cover_nhds converSetMap
  intro S
  have funcongr: Set.EqOn
    (fun x â†¦ âˆ‘' (pq : â„• Ã— â„•), â†‘(Jâ‚š pq) * smStep Î¼ (x - (pq.1 * s + pq.2 * t)))
    (fun x â†¦ âˆ‘ pq âˆˆ Î›ceiled s t (S + 1), â†‘(Jâ‚š pq) * smStep Î¼ (x - (pq.1 * s + pq.2 * t)))
    (coverSet S) := by
    intro x xmem
    unfold coverSet at xmem
    simp only [Set.mem_Iio] at xmem
    simp only
    apply tsum_eq_sum
    intro pq pqnotmem
    unfold Î›ceiled at pqnotmem
    simp only [Set.mem_toFinset, Set.mem_setOf_eq, not_le] at pqnotmem
    apply mul_eq_zero_of_right
    unfold smStep
    have cond: x - (pq.1 * s + pq.2 * t) â‰¤ 0 := sub_nonpos_of_le (le_of_lt (lt_trans xmem pqnotmem))
    simp only [cond, â†“reduceIte]
  refine ContinuousOn.congr ?_ funcongr
  apply Continuous.continuousOn
  apply continuous_finset_sum
  intro pq pqmem
  apply Continuous.mul
  Â· exact continuous_const
  Â· apply Continuous.comp (smStepContinuous Î¼)
    fun_prop

lemma Ï†RegContinuousAt (s t Î¼ Ïƒ x: â„) (hx: x â‰  0) [PosReal s] [PosReal t] [PosReal Î¼]:
ContinuousAt (Ï†Reg s t Î¼ Ïƒ) x := by
  obtain neg|pos := lt_or_gt_of_ne hx
  Â· apply Filter.EventuallyEq.continuousAt (y := 0)
    apply eventually_nhds_iff.mpr
    use Set.Iio 0
    constructor
    Â· intro y ymem
      simp only [Set.mem_Iio] at ymem
      rw [Ï†Reg_neg _ _ _ _ _ ymem]
    Â· exact âŸ¨isOpen_Iio, negâŸ©
  Â· apply ContinuousAt.mul (by fun_prop)
    apply ContinuousAt.add
    Â· apply Filter.EventuallyEq.continuousAt (y := 1)
      apply eventually_nhds_iff.mpr
      use Set.Ioi 0
      constructor
      Â· intro y ymem
        simp only [Set.mem_Ioi] at ymem
        obtain ymem' := le_of_lt ymem
        simp only [Set.mem_Ici, ymem', Set.indicator_of_mem]
      Â· exact âŸ¨isOpen_Ioi, posâŸ©
    Â· refine ContinuousOn.continuousAt (s := Set.Ioo 0 (x + 1)) ?_ (Ioo_mem_nhds pos (lt_add_one x))
      apply Continuous.continuousOn
      apply JceiledContinuous


lemma Ï†RegMeasurable (s t Î¼ Ïƒ: â„) [PosReal s] [PosReal t] [PosReal Î¼]:
Measurable (Ï†Reg s t Î¼ Ïƒ) := by
  apply Measurable.mul
  Â· apply Continuous.measurable
    continuity
  Â· apply Measurable.add
    Â· apply Measurable.indicator
      Â· measurability
      Â· exact measurableSet_Ici
    Â· apply Continuous.measurable
      apply JceiledContinuous

noncomputable
def Ï†RegFourierIntegrant (s t Î¼ Ïƒ f x: â„): â„‚ :=
  cexp ((-2 * Ï€ * f * x: â„) * I) * (rexp (- Ïƒ * x) * (Set.indicator (Set.Ici 0) (fun _ â†¦ 1) x + âˆ‘' pq, Jâ‚š pq * (smStep Î¼ (x - (pq.1 * s + pq.2 * t)))): â„)

lemma Ï†Reg_Fourier1 (s t Î¼ Ïƒ f: â„):
ğ“• (fun x â†¦ (Ï†Reg s t Î¼ Ïƒ x:â„‚)) f =
âˆ« (x:â„), Ï†RegFourierIntegrant s t Î¼ Ïƒ f x := by
  unfold Ï†Reg Ï†RegFourierIntegrant
  rw [fourierIntegral_eq']
  congr
  ext x
  congr 1
  congr 1
  simp only [RCLike.inner_apply, conj_trivial]
  push_cast
  ring

lemma Ï†Reg_FourierIntegrable (s t Î¼ Ïƒ f: â„) (ÏƒBound: Real.log 2 / (s âŠ“ t) < Ïƒ) [PosReal s] [PosReal t] [PosReal Î¼]:
Integrable (Ï†RegFourierIntegrant s t Î¼ Ïƒ f) := by
  have expIntegrable: IntegrableOn (fun x â†¦ rexp ((Real.log 2 / (min s t) - Ïƒ) * x) * rexp (Real.log 2 / (min s t) * max s t)) (Set.Ioi 0) volume := by
    apply Integrable.mul_const
    rw [(by ring: (Real.log 2 / (s âŠ“ t) - Ïƒ) = -(Ïƒ - Real.log 2 / (s âŠ“ t)))]
    apply exp_neg_integrableOn_Ioi
    exact sub_pos_of_lt ÏƒBound
  have integrableOn: IntegrableOn (Ï†RegFourierIntegrant s t Î¼ Ïƒ f) (Set.Ioi 0) := by
    unfold Ï†RegFourierIntegrant
    apply MeasureTheory.Integrable.mono' expIntegrable
    Â· apply MeasureTheory.AEStronglyMeasurable.mul
      Â· apply MeasureTheory.StronglyMeasurable.aestronglyMeasurable
        apply Continuous.stronglyMeasurable
        continuity
      Â· rw [(by rfl: (fun x â†¦((
          (rexp (-Ïƒ * x) * ((Set.Ici 0).indicator (fun x â†¦ 1) x + âˆ‘' (pq : â„• Ã— â„•), (Jâ‚š pq) * smStep Î¼ (x - (pq.1 * s + pq.2 * t)))):â„):â„‚)) =
          Complex.ofReal âˆ˜ Ï†Reg s t Î¼ Ïƒ)]
        apply AEStronglyMeasurable.comp_measurable
        Â· apply MeasureTheory.StronglyMeasurable.aestronglyMeasurable
          exact Continuous.stronglyMeasurable continuous_ofReal
        Â· apply Ï†RegMeasurable
    Â· apply Filter.Eventually.of_forall
      intro x
      rw [norm_mul]
      rw [(by apply norm_exp_ofReal_mul_I: â€–cexp ((-2 * Ï€ * f * x: â„) * I)â€– = 1), one_mul]
      rw [Complex.norm_real, norm_eq_abs]
      rw [â† Ï†Reg]
      rw [abs_of_nonneg (by apply Ï†RegNonneg)]
      apply Ï†RegBound
  apply MeasureTheory.IntegrableOn.integrable_of_ae_notMem_eq_zero integrableOn
  simp only [Set.mem_Ioi, not_lt]
  suffices âˆ€áµ (x : â„), x âˆˆ Set.Iic 0 â†’ Ï†RegFourierIntegrant s t Î¼ Ïƒ f x = 0 by exact this
  rw [â† MeasureTheory.ae_restrict_iff' measurableSet_Iic,
    MeasureTheory.Measure.restrict_congr_set MeasureTheory.Iio_ae_eq_Iic.symm,
    MeasureTheory.ae_restrict_iff' measurableSet_Iio]
  apply Filter.Eventually.of_forall
  intro x xmem
  simp only [Set.mem_Iio] at xmem
  unfold Ï†RegFourierIntegrant
  apply mul_eq_zero_of_right
  norm_cast
  rw [â† Ï†Reg]
  exact Ï†Reg_neg _ _ _ _ _ xmem

noncomputable
def Ï†RegFourierIntegrantLeft (Ïƒ f x: â„) :=
  Set.indicator (Set.Ici 0) (fun (x:â„) â†¦ cexp (-(2 * Ï€ * f * I + Ïƒ) * x)) x

noncomputable
def Ï†RegFourierIntegrantRight (s t Î¼ Ïƒ f x: â„) :=
  âˆ‘' pq, Jâ‚š pq * cexp (-(2 * Ï€ * f * I + Ïƒ) * x) * (smStep Î¼ (x - (pq.1 * s + pq.2 * t)))

lemma indicator_cast {s: Set Î±} {f : Î± â†’ â„} {x: Î±}:
  ((s.indicator f x: â„): â„‚) = s.indicator (fun a â†¦ ((f a): â„‚)) x := by
  obtain k := AddMonoidHom.map_indicator Complex.ofRealHom.toAddMonoidHom s f x
  simp only [RingHom.toAddMonoidHom_eq_coe, AddMonoidHom.coe_coe, ofRealHom_eq_coe] at k
  exact k


lemma Ï†RegFourierIntegrantRw1 (s t Î¼ Ïƒ f x: â„):
Ï†RegFourierIntegrant s t Î¼ Ïƒ f x =
Ï†RegFourierIntegrantLeft Ïƒ f x + Ï†RegFourierIntegrantRight s t Î¼ Ïƒ f x := by
  unfold Ï†RegFourierIntegrant
  push_cast
  rw [â† mul_assoc]
  rw [â† Complex.exp_add]
  rw [(by ring: -2 * Ï€ * f * x * I + -Ïƒ * x = -(2 * Ï€ * f * I + Ïƒ) * x)]
  rw [mul_add]
  congr 1
  Â· unfold Ï†RegFourierIntegrantLeft
    rw [indicator_cast]
    rw [â† Set.indicator_mul_right _ (fun (x: â„) â†¦ cexp (-(2 * Ï€ * f * I + Ïƒ) * x))]
    simp only [ofReal_one, mul_one]
  Â· unfold Ï†RegFourierIntegrantRight
    rw [â† tsum_mul_left]
    congr
    ext pq
    ring

lemma integrable_exp_mul_complex_Ioi {a: â„} {c : â„‚} (hc : c.re < 0):
IntegrableOn (fun (x: â„) â†¦ Complex.exp (c * x)) (Set.Ioi a) := by
  refine (integrable_norm_iff ?_).mp ?_
  Â· apply Continuous.aestronglyMeasurable
    fun_prop
  Â· simp_rw [Complex.norm_exp]
    simp only [mul_re, ofReal_re, ofReal_im, mul_zero, sub_zero]
    set b := -c.re
    rw [(by unfold b; simp only [neg_neg]: c.re = -b)]
    apply exp_neg_integrableOn_Ioi
    unfold b
    exact Left.neg_pos_iff.mpr hc

lemma rexp_mul_n (x: â„) (n: â„•): rexp (x * n) = (rexp x) ^ n := by
  rw [Real.exp_mul]
  simp only [rpow_natCast]

lemma Ï†Reg_Fourier2 (s t Î¼ Ïƒ f: â„) (ÏƒBound: Real.log 2 / (s âŠ“ t) < Ïƒ) [PosReal s] [PosReal t] [PosReal Î¼]:
ğ“• (fun x â†¦ (Ï†Reg s t Î¼ Ïƒ x:â„‚)) f =
(2 * Ï€ * f * I + Ïƒ)â»Â¹ + âˆ« (x:â„), Ï†RegFourierIntegrantRight s t Î¼ Ïƒ f x := by
  have Ïƒpos: 0 < Ïƒ:= by
    refine lt_trans ?_ ÏƒBound
    apply div_pos (log_pos (by norm_num))
    simp only [lt_inf_iff]
    exact âŸ¨PosReal.pos, PosReal.posâŸ©
  have leftIntegrable: MeasureTheory.Integrable (Ï†RegFourierIntegrantLeft Ïƒ f) := by
    unfold Ï†RegFourierIntegrantLeft
    apply (MeasureTheory.integrable_indicator_iff measurableSet_Ici).mpr
    apply (integrableOn_Ici_iff_integrableOn_Ioi (by simp)).mpr
    apply (MeasureTheory.integrable_norm_iff (by apply Continuous.aestronglyMeasurable; fun_prop)).mp
    have exprw: (fun (x:â„) â†¦ â€–cexp (-(2 * Ï€ * f * I + Ïƒ) * x)â€–) = fun (x:â„) â†¦ rexp (-Ïƒ * x) := by
      ext x
      rw [neg_add, add_mul, Complex.exp_add, norm_mul]
      rw [(by push_cast; ring: (-(2 * Ï€ * f * I) * x: â„‚) = (-2 * Ï€ * f * x: â„) * I)]
      rw [Complex.norm_exp_ofReal_mul_I, one_mul]
      norm_cast
      exact norm_of_nonneg (exp_nonneg _)
    rw [exprw]
    exact exp_neg_integrableOn_Ioi 0 Ïƒpos

  have rightIntegrable: MeasureTheory.Integrable (Ï†RegFourierIntegrantRight s t Î¼ Ïƒ f) := by
    have subeq: Ï†RegFourierIntegrantRight s t Î¼ Ïƒ f = Ï†RegFourierIntegrant s t Î¼ Ïƒ f - Ï†RegFourierIntegrantLeft Ïƒ f := by
      ext x
      simp only [Pi.sub_apply]
      rw [Ï†RegFourierIntegrantRw1]
      ring
    rw [subeq]
    apply MeasureTheory.Integrable.sub (Ï†Reg_FourierIntegrable s t Î¼ Ïƒ f ÏƒBound) leftIntegrable
  rw [Ï†Reg_Fourier1]
  simp_rw [Ï†RegFourierIntegrantRw1]
  rw [integral_add leftIntegrable rightIntegrable]
  apply add_right_cancel_iff.mpr
  unfold Ï†RegFourierIntegrantLeft
  rw [MeasureTheory.integral_indicator measurableSet_Ici]
  rw [MeasureTheory.integral_Ici_eq_integral_Ioi]
  rw [integral_exp_mul_complex_Ioi ?_ _]
  Â· simp only [neg_add_rev, ofReal_zero, mul_zero, Complex.exp_zero]
    rw [neg_div, â† div_neg]
    rw [one_div]
    congr
    simp only [neg_add_rev, neg_neg]
  Â· simp only [neg_add_rev, add_re, neg_re, ofReal_re, mul_re, re_ofNat, im_ofNat, ofReal_im,
    mul_zero, sub_zero, mul_im, zero_mul, add_zero, I_re, I_im, mul_one, sub_self, neg_zero,
    Left.neg_neg_iff]
    exact Ïƒpos



noncomputable
def Ï†RegFourierIntegrantRightSummand (Î´ Î¼: â„) (l: â„‚) :=
  âˆ« (x:â„), cexp (l * x) * (smStep Î¼ (x - Î´))

lemma Ï†RegFourierIntegrantRightExchange (s t Î¼ Ïƒ f: â„) (ÏƒBound: Real.log 2 / (s âŠ“ t) < Ïƒ) [PosReal s] [PosReal t] [PosReal Î¼]:
âˆ« (x:â„), Ï†RegFourierIntegrantRight s t Î¼ Ïƒ f x = âˆ‘' pq, Jâ‚š pq * Ï†RegFourierIntegrantRightSummand (pq.1 * s + pq.2 * t) Î¼ (-(2 * Ï€ * f * I + Ïƒ)) := by
  have Ïƒpos: 0 < Ïƒ:= by
    refine lt_trans ?_ ÏƒBound
    apply div_pos (log_pos (by norm_num))
    simp only [lt_inf_iff]
    exact âŸ¨PosReal.pos, PosReal.posâŸ©
  have ÏƒBound': Real.log 2 < Ïƒ * (s âŠ“ t) := by
    refine (div_lt_iffâ‚€ ?_).mp ÏƒBound;
    simp only [lt_inf_iff]
    exact âŸ¨PosReal.pos, PosReal.posâŸ©
  rw [mul_min_of_nonneg _ _ (le_of_lt Ïƒpos)] at ÏƒBound'
  obtain âŸ¨sBound, tBoundâŸ© := lt_inf_iff.mp ÏƒBound'
  unfold Ï†RegFourierIntegrantRight Ï†RegFourierIntegrantRightSummand
  simp_rw [â† integral_const_mul, â† mul_assoc]
  symm
  apply MeasureTheory.integral_tsum_of_summable_integral_norm
  Â· rintro âŸ¨p, qâŸ©
    conv in (fun x â†¦ _) =>
      intro x
      rw [mul_assoc]
    apply Integrable.const_mul
    have cexpIntegrable: IntegrableOn (fun (x: â„) â†¦ cexp (-(2 * Ï€ * f * I + Ïƒ) * x)) (Set.Ioi 0) := by
      apply integrable_exp_mul_complex_Ioi
      simp only [neg_add_rev, add_re, neg_re, ofReal_re, mul_re, re_ofNat, im_ofNat, ofReal_im,
        mul_zero, sub_zero, mul_im, zero_mul, add_zero, I_re, I_im, mul_one, sub_self, neg_zero,
        Left.neg_neg_iff]
      exact Ïƒpos
    obtain cexpIntegrable' := Integrable.norm cexpIntegrable
    refine MeasureTheory.IntegrableOn.integrable_of_ae_notMem_eq_zero (s := (Set.Ioi 0)) ?_ ?_
    Â· apply Integrable.mono' cexpIntegrable'
      Â· apply Continuous.aestronglyMeasurable
        apply Continuous.mul (by fun_prop)
        exact Continuous.comp' continuous_ofReal (Continuous.comp' (smStepContinuous Î¼) (by fun_prop))
      Â· apply Filter.Eventually.of_forall
        intro x
        rw [norm_mul]
        apply (mul_le_of_le_one_right (by apply norm_nonneg))
        norm_cast
        rw [norm_eq_abs]
        rw [abs_eq_self.mpr (by apply smStepNonneg)]
        apply smStepLe1
    Â· apply Filter.Eventually.of_forall
      intro x xmem
      simp only [Set.mem_Ioi, not_lt] at xmem
      apply mul_eq_zero_of_right
      norm_cast
      have cond: x - (p * s + q * t) â‰¤ 0 := by
        apply sub_nonpos_of_le
        apply le_trans xmem
        apply add_nonneg
        all_goals exact mul_nonneg (by apply Nat.cast_nonneg) (le_of_lt PosReal.pos)
      unfold smStep
      simp only [cond, â†“reduceIte]
  Â· have normBound: âˆ€ (pq:â„•Ã—â„•), norm (âˆ« (x : â„), â€–(Jâ‚š pq) * cexp (-(2 * Ï€ * f * I + Ïƒ) * x) * (smStep Î¼ (x - (pq.1 * s + pq.2 * t)))â€–)
      â‰¤ 2 ^ pq.1 * 2 ^ pq.2 * (rexp (-Ïƒ * (pq.1 * s + pq.2 * t)) / Ïƒ) := by
      intro pq
      conv in (fun x â†¦ _) =>
        intro x
        rw [mul_assoc, norm_mul]
      rw [integral_const_mul]
      rw [norm_mul]
      refine mul_le_mul ?_ ?_ (by apply norm_nonneg) ?_
      Â· norm_cast
        apply Jâ‚š_bound
      Â· rw [norm_eq_abs]
        rw [abs_eq_self.mpr (by apply integral_nonneg; intro x; simp only [Pi.zero_apply]; apply norm_nonneg)]
        simp_rw [norm_mul, norm_exp]
        have union: (Set.univ: Set â„) = Set.Ioi (pq.1 * s + pq.2 * t)âˆª Set.Iic (pq.1 * s + pq.2 * t) := by
          rw [Set.union_comm]; simp only [Set.Iic_union_Ioi]
        rw [â† MeasureTheory.setIntegral_univ, union]
        rw [MeasureTheory.integral_union_eq_left_of_forall measurableSet_Iic ?_]
        Â· have rightrw: rexp (-Ïƒ * (pq.1 * s + pq.2 * t)) / Ïƒ = âˆ« (x : â„) in Set.Ioi (pq.1 * s + pq.2 * t), Real.exp (-Ïƒ * x) := by
            symm
            apply Complex.ofReal_inj.mp
            convert integral_exp_mul_complex_Ioi (a := -Ïƒ) ?_ (pq.1 * s + pq.2 * t)
            Â· norm_cast
              exact Eq.symm integral_complex_ofReal
            Â· norm_cast
              simp only [neg_mul, neg_div_neg_eq]
            Â· simp only [neg_re, ofReal_re, Left.neg_neg_iff]
              exact Ïƒpos
          rw [rightrw]
          gcongr
          Â· refine Integrable.mono' (g := fun x â†¦ Real.exp (-Ïƒ * x)) (exp_neg_integrableOn_Ioi _ Ïƒpos) ?_ ?_
            Â· apply Continuous.aestronglyMeasurable
              apply Continuous.mul (by fun_prop)
              exact Continuous.comp' continuous_norm (
                Continuous.comp' continuous_ofReal (Continuous.comp' (smStepContinuous Î¼) (by fun_prop)))
            Â· apply Filter.Eventually.of_forall
              intro x
              simp only [neg_add_rev, mul_re, add_re, neg_re, ofReal_re, re_ofNat, im_ofNat,
                ofReal_im, mul_zero, sub_zero, mul_im, zero_mul, add_zero, I_re, I_im, mul_one,
                sub_self, neg_zero, neg_mul, add_im, neg_im, zero_add, norm_real, norm_eq_abs,
                norm_mul, Real.abs_exp, abs_abs]
              apply (mul_le_of_le_one_right (by apply exp_nonneg))
              rw [abs_eq_self.mpr (by apply smStepNonneg)]
              apply smStepLe1
          Â· exact exp_neg_integrableOn_Ioi _ Ïƒpos
          Â· intro x
            simp only [neg_add_rev, mul_re, add_re, neg_re, ofReal_re, re_ofNat, im_ofNat,
              ofReal_im, mul_zero, sub_zero, mul_im, zero_mul, add_zero, I_re, I_im, mul_one,
              sub_self, neg_zero, neg_mul, add_im, neg_im, zero_add, norm_real, norm_eq_abs]
            apply (mul_le_of_le_one_right (by apply exp_nonneg))
            rw [abs_eq_self.mpr (by apply smStepNonneg)]
            apply smStepLe1
        Â· intro x xmem
          simp only [Set.mem_Iic] at xmem
          apply mul_eq_zero_of_right
          simp only [norm_real, norm_eq_abs, abs_eq_zero]
          unfold smStep
          obtain cond := sub_nonpos_of_le xmem
          simp only [cond, â†“reduceIte]
      Â· apply mul_nonneg
        all_goals
        Â· apply pow_nonneg
          Â· norm_num
    refine Summable.of_norm_bounded ?_ normBound

    have summandRw: (fun (pq: â„• Ã— â„•) â†¦ 2 ^ pq.1 * 2 ^ pq.2 * (rexp (-Ïƒ * (pq.1 * s + pq.2 * t)) / Ïƒ))
     = fun (pq: â„• Ã— â„•) â†¦ ((2 * rexp (-Ïƒ * s)) ^ pq.1 * (2 * rexp (-Ïƒ * t)) ^ pq.2) / Ïƒ := by
      ext pq
      rw [mul_pow, mul_pow]
      rw [â† rexp_mul_n, â† rexp_mul_n]
      rw [mul_add, Real.exp_add]
      field_simp
      ring_nf

    rw [summandRw]
    apply Summable.div_const
    apply Summable.mul_of_nonneg
    Â· simp only [neg_mul, summable_geometric_iff_norm_lt_one, norm_mul, Real.norm_ofNat,
        norm_eq_abs, Real.abs_exp]
      rw [Real.exp_neg]
      apply (mul_inv_lt_iffâ‚€ (by apply exp_pos)).mpr
      simp only [one_mul]
      apply (log_lt_iff_lt_exp (by norm_num)).mp
      exact sBound
    Â· simp only [neg_mul, summable_geometric_iff_norm_lt_one, norm_mul, Real.norm_ofNat,
        norm_eq_abs, Real.abs_exp]
      rw [Real.exp_neg]
      apply (mul_inv_lt_iffâ‚€ (by apply exp_pos)).mpr
      simp only [one_mul]
      apply (log_lt_iff_lt_exp (by norm_num)).mp
      exact tBound
    Â· intro p
      simp only [Pi.zero_apply]
      apply pow_nonneg
      apply mul_nonneg
      Â· norm_num
      Â· apply exp_nonneg
    Â· intro p
      simp only [Pi.zero_apply]
      apply pow_nonneg
      apply mul_nonneg
      Â· norm_num
      Â· apply exp_nonneg

lemma Ï†RegFourierIntegrantRightSummandEq (Î´ Î¼: â„) (l: â„‚) (hl: l.re < 0) [PosReal Î¼]:
Ï†RegFourierIntegrantRightSummand Î´ Î¼ l = cexp (l * Î´) * (1 - cexp (l * Î¼)) / (l ^ 2 * Î¼) := by
  rw [mul_sub, â† Complex.exp_add, â† mul_add]

  unfold Ï†RegFourierIntegrantRightSummand
  have union: (Set.univ: Set â„) = (Set.Ioc Î´ (Î´ + Î¼) âˆª Set.Ioi (Î´ + Î¼)) âˆª Set.Iic Î´ := by
    rw [Set.union_comm];
    rw [Set.Ioc_union_Ioi_eq_Ioi ((le_add_iff_nonneg_right Î´).mpr (le_of_lt PosReal.pos))]
    simp only [Set.Iic_union_Ioi]
  rw [â† MeasureTheory.setIntegral_univ, union]
  have leftZero: âˆ€ x âˆˆ Set.Iic Î´, cexp (l * â†‘x) * smStep Î¼ (x - Î´) = 0 := by
    intro x xmem
    simp only [Set.mem_Iic] at xmem
    apply mul_eq_zero_of_right
    norm_cast
    unfold smStep
    obtain cond := sub_nonpos_of_le xmem
    simp only [cond, â†“reduceIte]

  rw [MeasureTheory.integral_union_eq_left_of_forall measurableSet_Iic leftZero]

  have Î¼0: (Î¼ : â„‚) â‰  0 := by norm_cast; exact ne_of_gt PosReal.pos
  have l0: l â‰  0 := ne_zero_of_re_neg hl
  have l2Î¼0: l ^ 2 * Î¼ â‰  0 := by
    refine mul_ne_zero ?_ Î¼0
    simp only [ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true, pow_eq_zero_iff]
    exact l0


  have Leftfcongr: (fun (x: â„) â†¦ (cexp (l * x) * smStep Î¼ (x - Î´): â„‚)) =áµ[volume.restrict (Set.Ioc Î´ (Î´ + Î¼))]
    (fun (x: â„) â†¦ (cexp (l * x) * ((x - Î´) / Î¼): â„‚)) := by
    apply (MeasureTheory.ae_restrict_iff' (by exact measurableSet_Ioc)).mpr
    apply Filter.Eventually.of_forall
    intro x xmem
    obtain âŸ¨c1, c2âŸ© := xmem
    simp only [mul_eq_mul_left_iff, Complex.exp_ne_zero, or_false]
    unfold smStep
    have cond1: Â¬ x - Î´ â‰¤ 0 := by simp only [tsub_le_iff_right, zero_add, not_le]; exact c1
    have cond2: x - Î´ â‰¤ Î¼ := by exact tsub_le_iff_left.mpr c2
    simp only [cond1, cond2, â†“reduceIte]
    norm_cast

  have leftIntegral: âˆ« (x : â„) in Set.Ioc Î´ (Î´ + Î¼), cexp (l * x) * smStep Î¼ (x - Î´) =
      (cexp (l * (Î´ + Î¼)) * (l * Î¼ - 1) + cexp (l * Î´)) / (l ^ 2 * Î¼) := by
    rw [MeasureTheory.integral_congr_ae Leftfcongr]
    have der (x: â„): HasDerivAt (fun (x: â„) â†¦ cexp (l * x) * ((l * x - l * Î´ - 1) / (l ^ 2 * Î¼): â„‚)) (cexp (l * x) * ((x - Î´) / Î¼): â„‚) x := by
      rw [(by field_simp [Î¼0, l2Î¼0]; ring:
        (cexp (l * x) * ((x - Î´) / Î¼): â„‚) =
        ((l * 1) * cexp (l * x) * ((l * x - l * Î´ - 1) / (l ^ 2 * Î¼)): â„‚) + (cexp (l * x) * ((l * 1) / (l^2 * Î¼)): â„‚))]
      apply HasDerivAt.mul
      Â· rw [mul_comm]
        apply (Complex.hasDerivAt_exp _).comp x _
        exact ((hasDerivAt_id (x : â„‚)).const_mul _).comp_ofReal
      Â· apply HasDerivAt.div_const
        simp only [hasDerivAt_sub_const_iff]
        exact ((hasDerivAt_id (x : â„‚)).const_mul _).comp_ofReal
    rw [â† intervalIntegral.integral_of_le ((le_add_iff_nonneg_right Î´).mpr (le_of_lt PosReal.pos))]
    rw [intervalIntegral.integral_deriv_eq_sub' _ (funext fun x => (der x).deriv) (fun x _ => (der x).differentiableAt) (by fun_prop)]
    field_simp [l2Î¼0]
    ring


  have Rightfcongr: (fun (x: â„) â†¦ (cexp (l * x) * smStep Î¼ (x - Î´): â„‚)) =áµ[volume.restrict (Set.Ioi (Î´ + Î¼))]
    (fun (x: â„) â†¦ cexp (l * x)) := by
    apply (MeasureTheory.ae_restrict_iff' (by exact measurableSet_Ioi)).mpr
    apply Filter.Eventually.of_forall
    intro x xmem
    simp only [Set.mem_Ioi] at xmem
    have cond2: Â¬ x - Î´ â‰¤ Î¼ := by rw [not_le]; exact lt_tsub_iff_left.mpr xmem
    have cond1: Â¬ x - Î´ â‰¤ 0 := by
      rw [not_le]
      obtain c2 := not_le.mp cond2
      exact lt_trans PosReal.pos c2
    unfold smStep
    simp only [cond1, cond2, â†“reduceIte, ofReal_one, mul_one]

  have rightIntegral: âˆ« (x : â„) in Set.Ioi (Î´ + Î¼), cexp (l * x) * smStep Î¼ (x - Î´) =
      -cexp (l * (Î´ + Î¼)) / l := by
    rw [MeasureTheory.integral_congr_ae Rightfcongr]
    rw [integral_exp_mul_complex_Ioi hl _]
    norm_cast

  rw [(by field_simp [l0, l2Î¼0]; ring:
    (cexp (l * Î´) * 1 - cexp (l * (Î´ + Î¼))) / (l ^ 2 * Î¼) =
    (cexp (l * (Î´ + Î¼)) * (l * Î¼ - 1: â„‚) + cexp (l * Î´)) / (l ^ 2 * Î¼) + (-cexp (l * (Î´ + Î¼)) / l))]

  rw [â† leftIntegral, â† rightIntegral]
  apply MeasureTheory.setIntegral_union
  Â· simp only [le_refl, Set.Ioc_disjoint_Ioi]
  Â· exact measurableSet_Ioi
  Â· refine MeasureTheory.IntegrableOn.congr_fun_ae ?_ Leftfcongr.symm
    apply Continuous.integrableOn_Ioc
    fun_prop
  Â· refine MeasureTheory.IntegrableOn.congr_fun_ae ?_ Rightfcongr.symm
    apply integrable_exp_mul_complex_Ioi
    exact hl

noncomputable
def Ï†RegFourierResult (s t Î¼ Ïƒ f: â„) := (2 * Ï€ * f * I + Ïƒ)â»Â¹ +
  (1 - (cexp (-(2 * Ï€ * f * I + Ïƒ) * s) + cexp (-(2 * Ï€ * f * I + Ïƒ) * t)))â»Â¹ *
  ((1 - cexp (-(2 * Ï€ * f * I + Ïƒ) * Î¼)) / ((2 * Ï€ * f * I + Ïƒ) ^ 2 * Î¼))

lemma Ï†Reg_Fourier (s t Î¼ Ïƒ: â„) (ÏƒBound: Real.log 2 / (s âŠ“ t) < Ïƒ) [PosReal s] [PosReal t] [PosReal Î¼]:
ğ“• (fun x â†¦ (Ï†Reg s t Î¼ Ïƒ x:â„‚)) = Ï†RegFourierResult s t Î¼ Ïƒ := by
  ext f
  have Ïƒpos: 0 < Ïƒ:= by
    refine lt_trans ?_ ÏƒBound
    apply div_pos (log_pos (by norm_num))
    simp only [lt_inf_iff]
    exact âŸ¨PosReal.pos, PosReal.posâŸ©
  have ÏƒBound': Real.log 2 < Ïƒ * (s âŠ“ t) := by
    refine (div_lt_iffâ‚€ ?_).mp ÏƒBound;
    simp only [lt_inf_iff]
    exact âŸ¨PosReal.pos, PosReal.posâŸ©
  rw [mul_min_of_nonneg _ _ (le_of_lt Ïƒpos)] at ÏƒBound'
  obtain âŸ¨sBound, tBoundâŸ© := lt_inf_iff.mp ÏƒBound'
  rw [Ï†Reg_Fourier2 _ _ _ _ _ ÏƒBound]
  rw [Ï†RegFourierIntegrantRightExchange _ _ _ _ _ ÏƒBound]
  have h: (-(2 * Ï€ * f * I + Ïƒ)).re < 0 := by
    simp only [neg_add_rev, add_re, neg_re, ofReal_re, mul_re, re_ofNat, im_ofNat, ofReal_im,
      mul_zero, sub_zero, mul_im, zero_mul, add_zero, I_re, I_im, mul_one, sub_self, neg_zero,
      Left.neg_neg_iff]
    exact Ïƒpos
  conv in (fun pq â†¦ _) =>
    ext pq
    rw [Ï†RegFourierIntegrantRightSummandEq _ _ _ h]
    rw [Complex.ofReal_add, mul_comm _ s, mul_comm _ t, Complex.ofReal_mul, Complex.ofReal_mul,
       mul_add, Complex.exp_add, â† mul_assoc _ (s:â„‚) _, â† mul_assoc _ (t:â„‚) _, ]
    simp only [ofReal_natCast]
    rw [mul_comm _ (pq.1:â„‚), mul_comm _ (pq.2:â„‚), Complex.exp_nat_mul, Complex.exp_nat_mul]
    rw [â† mul_div, â† mul_assoc, â† mul_assoc]
  rw [tsum_mul_right]
  rw [neg_pow_two]
  congr
  refine (bigeometric_series (cexp (-(2 * Ï€ * f * I + Ïƒ) * s)) (cexp (-(2 * Ï€ * f * I + Ïƒ) * t)) ?_ ?_).tsum_eq
  all_goals
  Â· rw [Complex.norm_exp]
    simp only [neg_add_rev, mul_re, add_re, neg_re, ofReal_re, re_ofNat, im_ofNat, ofReal_im,
      mul_zero, sub_zero, mul_im, zero_mul, add_zero, I_re, I_im, mul_one, sub_self, neg_zero,
      neg_mul, add_im, neg_im, zero_add]
    rw [Real.exp_neg]
    refine (inv_lt_invâ‚€ (by apply exp_pos) (by norm_num)).mpr ?_
    refine (log_lt_iff_lt_exp (by norm_num)).mp ?_
    try exact sBound
    try exact tBound

/-
lemma Ï†Reg_FourierInv (s t Î¼ Ïƒ x: â„) (ÏƒBound: Real.log 2 / (s âŠ“ t) < Ïƒ) (xBound: 0 < x)
[PosReal s] [PosReal t] [PosReal Î¼]:
ğ“•â» (Ï†RegFourierResult s t Î¼ Ïƒ) x = Ï†Reg s t Î¼ Ïƒ x := by
  rw [â† Ï†Reg_Fourier _ _ _ _ ÏƒBound]
  apply MeasureTheory.Integrable.fourier_inversion
  Â· obtain finteg := Ï†Reg_FourierIntegrable s t Î¼ Ïƒ 0 ÏƒBound
    unfold Ï†RegFourierIntegrant at finteg
    conv at finteg in (fun x â†¦ _) =>
      ext x
      rw [(by simp only [neg_mul, mul_zero, zero_mul, ofReal_zero, Complex.exp_zero]:
        cexp ((-2 * Ï€ * 0 * x: â„) * I) = 1)]
      rw [one_mul]
    exact finteg
  Â· rw [Ï†Reg_Fourier _ _ _ _ ÏƒBound]
    unfold Ï†RegFourierResult
    sorry
  Â· apply ContinuousAt.comp continuous_ofReal.continuousAt
    apply Ï†RegContinuousAt
    exact ne_of_gt xBound
-/

def rootSet (s t: â„): Set â„‚ := sorry

noncomputable
def Ï†RegFourierDecompTerm (s t Î¼: â„) (z r: â„‚) :=
  (1 - cexp (-Î¼ * r)) / (Î¼ * r ^ 2 * (z - r) * (s * cexp (-s * r) + t * cexp (-t * r)))

lemma Ï†RegFourierDecomp (s t Î¼ Ïƒ f: â„):
Ï†RegFourierResult s t Î¼ Ïƒ f = âˆ‘' r: rootSet s t, Ï†RegFourierDecompTerm s t Î¼ (2 * Ï€ * f * I + Ïƒ) r := by
  sorry
